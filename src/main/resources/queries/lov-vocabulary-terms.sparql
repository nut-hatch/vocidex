PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX voaf: <http://purl.org/vocommons/voaf#>
PREFIX vann: <http://purl.org/vocab/vann/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

# ?term A term (class, property or datatype)
SELECT DISTINCT ?term {

  # Find all vocabularies, with the attached
  # preferred namespace URI, from LOV metadata
  GRAPH <https://lov.linkeddata.es/dataset/lov> {
    ?vocab a voaf:Vocabulary;
      vann:preferredNamespaceUri ?ns
  }

  # Cope with class, datatype, property and instance
  GRAPH ?vocab {
      ?term a ?type
        FILTER isURI(?term)
  }

  # Keep only those terms that start with the vocabulary's
  # preferred namespace URI, thus excluding cases where
  # a term is only mentioned, but not authoritatively
  # defined, in a graph
  FILTER (STRSTARTS(STR(?term), STR(?ns)))
}
